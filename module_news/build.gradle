if (isBuildModule.toBoolean()) {
    //作为独立App应用运行
    apply plugin: 'com.android.application'
} else {
    //作为组件运行
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'

android {
    compileSdkVersion build_versions.compile_sdk
    buildToolsVersion build_versions.build_tools

    defaultConfig {
        if (isBuildModule.toBoolean()) {
            applicationId "com.demo.mvvmdemo.module.news"
        }
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    sourceSets {
        main {
            if (isBuildModule.toBoolean()) {
                //独立运行
                res.srcDir 'src/main/alone/res/'
                manifest.srcFile 'src/main/alone/AndroidManifest.xml'
            } else {
                //合并到宿主
                manifest.srcFile 'src/main/AndroidManifest.xml'
                resources {
                    //正式版本时，排除alone文件夹下所有调试文件
                    exclude 'src/main/alone/*'
                }
            }
        }
    }
    //统一资源前缀，规范资源引用
    resourcePrefix "news_"
    dataBinding {
        enabled true
    }
}

dependencies {
    implementation project(':common')
    implementation deps.kotlin.kotlin_stdlib
    implementation deps.core_ktx
    implementation deps.app_compat
    implementation deps.material
    implementation deps.constraint_layout
    implementation deps.lifecycle.lifecycle_runtime
    implementation 'com.android.support:multidex:2.0.1'

}